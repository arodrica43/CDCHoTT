{-# OPTIONS --without-K #-}

{-
  Learnt about agda-flat from Ian Orton:
  (also Dan helped somewhere along the way)

  There is a branch of agda called flat, 
  which supports a comonadic modality called flat or â™­

  This file contains some experiments with â™­
  inspired by mike shulmans real-cohesion paper

  Starting with a 'let'-notation I learnd from Ian

  References to Lemmata and Theorems refer to Mike Shulman's Article

  https://arxiv.org/abs/1509.07584

-}


module CDCHoTT.Flat where
  open import CDCHoTT.Basics 
  open import CDCHoTT.EqualityAndPaths
  open import CDCHoTT.Homotopies
  open import CDCHoTT.Equivalences
  open import CDCHoTT.Contractibility
  open import CDCHoTT.DependentTypes

  data â™­ {@â™­ l : Level} (@â™­ A : ğ’° l) : ğ’° l where
    _^â™­ : (@â™­ a : A) â†’ â™­ A

  â™­-induction : âˆ€ {c : Level} {@â™­ l : Level}{@â™­ A : ğ’° l}
         â†’ (C : â™­ A â†’ ğ’° c)
         â†’ ((@â™­ u : A) â†’ C (u ^â™­))
         â†’ (x : â™­ A) â†’ C x
  â™­-induction C f (x ^â™­) = f x

  â™­-counit : âˆ€ {@â™­ l : Level} {@â™­ A : ğ’° l}
    â†’ (â™­ A â†’ A)
  â™­-counit (x ^â™­) = x

  â™­-counit-at : 
      âˆ€ (@â™­ A : ğ’°â‚€)
    â†’ (â™­ A â†’ A)
  â™­-counit-at A = â™­-counit {_} {A}

  _is-discrete : âˆ€ (@â™­ A : ğ’°â‚€) â†’ ğ’°â‚€
  A is-discrete = (â™­-counit-at A) is-an-equivalence
  
  â™­-idempotent : âˆ€ (@â™­ A : ğ’°â‚€)
    â†’ (â™­ A) is-discrete
  â™­-idempotent A =
    has-left-inverse
      (Î» {(x ^â™­) â†’ (x ^â™­) ^â™­})
      by (Î» {((x ^â™­) ^â™­) â†’ refl})
    and-right-inverse
      (Î» {(x ^â™­) â†’ x ^â™­ ^â™­})
      by (Î» {(x ^â™­) â†’ refl})

  letâ™­ :
    {@â™­ l l' : Level}
    {@â™­ A : ğ’° l}
    {C : â™­ A â†’ ğ’° l'}
    (s : â™­ A)
    (t : (@â™­ u : A) â†’ C (u ^â™­))
    â†’ -------------
    C s
  letâ™­ (a ^â™­) t = t a

  letâ™­' :
    {@â™­ l l' : Level}
    {@â™­ A : ğ’° l}
    {C : â™­ A â†’ ğ’° l'}
    (s : â™­ A)
    (t : (@â™­ u : A) â†’ C (u ^â™­))
    â†’ -------------
    C s
  letâ™­' {C = C} x t = letâ™­ {C = C} x t

  syntax letâ™­ s (Î» u â†’ t) = letâ™­ u ^â™­:= s inâ™­ t
  syntax letâ™­' {C = C} s (Î» u â†’ t) = letâ™­' u ^â™­:= s inâ™­ t in-family C


  â™­â†’ : âˆ€ {@â™­ A B : ğ’°â‚€}
    â†’ (@â™­ f : A â†’ B)
    â†’ (â™­ A â†’ â™­ B)
  â™­â†’ f (a ^â™­) = (f a) ^â™­

  â™­â†’-commutes-with-âˆ˜ : âˆ€ {@â™­ A B C : ğ’°â‚€}
    â†’ (@â™­ f : A â†’ B) (@â™­ g : B â†’ C)
    â†’ (â™­â†’ g) âˆ˜ (â™­â†’ f) â‡’ â™­â†’ (g âˆ˜ f)
  â™­â†’-commutes-with-âˆ˜ f g (a ^â™­) = refl

  â™­-recursion :
    âˆ€ {@â™­ A B : ğ’°â‚€} 
    â†’ (@â™­ f : B â†’ A)
    â†’ (p : B is-discrete)
    â†’ (B â†’ â™­ A)
  â™­-recursion {B = B} f p = â™­â†’ f âˆ˜ Ï†
    where Ï† = left-inverse-of (â™­-counit-at B) given-by p

  â™­-identity-induction :
    âˆ€ {@â™­ A : ğ’°â‚€}
    â†’ (@â™­ C : (@â™­ x y : A) (@â™­ p : x â‰ˆ y) â†’ ğ’°â‚€)
    â†’ (@â™­ d : (@â™­ x : A) â†’ C x x refl)
    â†’ (@â™­ x y : A) â†’ (@â™­ p : x â‰ˆ y) â†’ C x y p
  â™­-identity-induction C d x .x refl = d x


  {- this can be done simpler that what is in Mike's article
      (thanks to Dan Licata for pointing this out to me)
     a proof close to what Mike did is further to the bottom 
     of this file -}
  â™­-commutes-with-identity-types :
    âˆ€ {@â™­ A : ğ’°â‚€}
    â†’ (@â™­ x y : A)
    â†’ â™­ (x â‰ˆ y) â‰ƒ x ^â™­ â‰ˆ y ^â™­
  â™­-commutes-with-identity-types x _ =
     (Î» {(refl ^â™­) â†’ refl})
    is-an-equivalence-because
      (has-left-inverse (Î» {refl â†’ refl ^â™­})
         by (Î» {(refl ^â™­) â†’ refl})
       and-right-inverse (Î» {refl â†’ refl ^â™­})
         by (Î» {refl â†’ refl}))

  {- Lemma 6.8 -}

  â™­-commutes-with-Î£ :
    âˆ€ (@â™­ A : ğ’°â‚€) (@â™­ B : A â†’ ğ’°â‚€)
    â†’ â™­ (Î£ A B) â‰ƒ Î£ (â™­ A) (Î» x â†’ letâ™­ u ^â™­:= x inâ™­ â™­ (B u))
  â™­-commutes-with-Î£ A B = (Î» {((a , b) ^â™­) â†’ (a ^â™­) , (b ^â™­)})
    is-an-equivalence-because
      (has-left-inverse (Î» {((a ^â™­) , (b ^â™­)) â†’ (a , b) ^â™­})
         by (Î» { ((a , b) ^â™­) â†’ refl })
       and-right-inverse (Î» {((a ^â™­) , (b ^â™­)) â†’ (a , b) ^â™­})
         by (Î» {((a ^â™­) , (b ^â™­)) â†’ refl }))


  â™­-apply :
    {@â™­ l : Level}
    {@â™­ A B : ğ’° l}
    (@â™­ f : A â†’ B)
    {@â™­ x y : A}
    (@â™­ p : x â‰ˆ y)
    â†’ f(x) â‰ˆ f(y)
  â™­-apply f refl = refl


  {- Theorem 6.10 -}

  private 
    glue-equivalences :
      {A : ğ’°â‚€}
      {P Q : A â†’ ğ’°â‚€}
      (p : (x : A) â†’ P x â‰ƒ Q x)
      â†’ Î£ A P â‰ƒ Î£ A Q
    glue-equivalences e =
      fiber-equivalences-along-an-equivalence-on-the-base.induced-map-as-equivalence
        _ _ id-as-equivalence e
    
  â™­-preserves-pullbacks :
    âˆ€ (@â™­ A B C : ğ’°â‚€) (@â™­ f : A â†’ C) (@â™­ g : B â†’ C)
    â†’ â™­ (Î£ A (Î» x â†’ Î£ B (Î» y â†’ f(x) â‰ˆ g(y))))
      â‰ƒ Î£ (â™­ A) (Î» x â†’ Î£ (â™­ B) (Î» y â†’ (â™­â†’ f)(x) â‰ˆ (â™­â†’ g)(y)))
  â™­-preserves-pullbacks A B C f g =
      â™­ (Î£ A (Î» x â†’ Î£ B (Î» y â†’ f(x) â‰ˆ g(y))))
    â‰ƒâŸ¨ â™­-commutes-with-Î£ A (Î» x â†’ âˆ‘ (Î» y â†’ f(x) â‰ˆ g(y))) âŸ©
      Î£ (â™­ A) (Î» x â†’ letâ™­ u ^â™­:= x inâ™­ â™­(Î£ B (Î» y â†’ f u â‰ˆ g y)))
    â‰ƒâŸ¨ glue-equivalences (Î» {(u ^â™­) â†’ â™­-commutes-with-Î£ B  (Î» y â†’ f u â‰ˆ g y)}) âŸ©
      Î£ (â™­ A) (Î» x â†’ letâ™­ u ^â™­:= x inâ™­ (Î£ (â™­ B) (Î» y â†’ letâ™­ v ^â™­:= y inâ™­ â™­ (f u â‰ˆ g v))))
    â‰ƒâŸ¨ glue-equivalences
         (Î» {(u ^â™­) â†’ glue-equivalences
           (Î» {(v ^â™­) â†’ â™­-commutes-with-identity-types (f u) (g v)}) }) âŸ©
      Î£ (â™­ A) (Î» x â†’ letâ™­ u ^â™­:= x inâ™­ (Î£ (â™­ B) (Î» y â†’ letâ™­ v ^â™­:= y inâ™­ ((f u) ^â™­ â‰ˆ (g v) ^â™­))))
    â‰ƒâŸ¨ glue-equivalences (Î» {(u ^â™­) â†’ glue-equivalences (Î» {(v ^â™­) â†’ id-as-equivalence})}) âŸ©
      Î£ (â™­ A) (Î» x â†’ letâ™­ u ^â™­:= x inâ™­ (Î£ (â™­ B) (Î» y â†’ (f u) ^â™­ â‰ˆ (â™­â†’ g) y)))
    â‰ƒâŸ¨ glue-equivalences (Î» {(u ^â™­) â†’ id-as-equivalence}) âŸ©
      Î£ (â™­ A) (Î» x â†’ Î£ (â™­ B) (Î» y â†’ (â™­â†’ f)(x) â‰ˆ (â™­â†’ g)(y)))
    â‰ƒâˆ








  -- setup for thm 6.1 and 'â™­-commutes-with-identity-types'
  â™­-encode-decode-is-enough :
    âˆ€ {@â™­ A : ğ’°â‚€} (code : â™­ A â†’ â™­ A â†’ ğ’°â‚€)
    â†’ (encode : (x y : â™­ A) â†’ x â‰ˆ y â†’ code x y)
    â†’ (decode : (x y : â™­ A) â†’ code x y â†’ x â‰ˆ y)
    â†’ (retract : (x y : â™­ A) â†’ (encode x y) âˆ˜ (decode x y) â‡’ id)
    â†’ (x y : â™­ A) â†’ (decode x y) is-an-equivalence
  â™­-encode-decode-is-enough {A} code encode decode retract x y =
    let
      step1 : (z : â™­ A) â†’ (Î£ _ (Î» w â†’ code z w)) is-contractible
      step1 z = retracts-of-contractibles-are-contractible
        (Î» {(w , c) â†’ (w , decode z w c)})
        (Î» {(w , Î³) â†’ (w , encode z w Î³)})
        (Î» {(w , c) â†’ (inclusion-of-the-fiber-of _ over w) â retract z w c})
        (J-in-terms-of-contractibilityâ€² (â™­ A) z)

      step2 : (z : â™­ A) â†’ (Î» {(w , c) â†’ (w , decode z w c)}) is-an-equivalence
      step2 z = the-map (Î» {(w , c) â†’ (w , decode z w c)}) is-an-equivalence-since-it-is-homotopic-to
        _ by
             maps-into-a-contractible-type-are-homotopic
               _ _ ((J-in-terms-of-contractibilityâ€² (â™­ A) z))
          which-is-an-equivalence-by
          (proof-of-equivalency (two-contractible-types-are-equivalent
            (step1 z) (J-in-terms-of-contractibilityâ€² (â™­ A) z) ))


    in equivalence-from-equivalence-on-sums.conclusion (decode x) (step2 x) y

  â™­-commutes-with-identity-types' :
    âˆ€ {@â™­ A : ğ’°â‚€}
    â†’ (@â™­ x y : A)
    â†’ â™­ (x â‰ˆ y) â‰ƒ x ^â™­ â‰ˆ y ^â™­
  â™­-commutes-with-identity-types' {A} x y =
    let
      {- from Mike's Real-Cohesion Article, section 6 -}
      code : â™­ A â†’ â™­ A â†’ ğ’°â‚€
      code = Î» {(z ^â™­) â†’ Î» {(w ^â™­) â†’ â™­ (z â‰ˆ w) }}

      step1 : code (x ^â™­) (y ^â™­) â‰ƒ â™­ (x â‰ˆ y)
      step1 = id-as-equivalence

      encode : (u v : â™­ A) â†’ u â‰ˆ v â†’ code u v
      encode u v Î³ = transport (Î» vâ€² â†’ code u vâ€²)  Î³
             ((Î» (u : â™­ A) â†’ letâ™­' x ^â™­:= u inâ™­ (refl ^â™­) in-family (Î» uâ€² â†’ code uâ€² uâ€²)) u)

      decode : (u v : â™­ A) â†’ code u v â†’ u â‰ˆ v
      decode = Î» {(x ^â™­) (y ^â™­) (refl ^â™­) â†’ refl }

      step2 : code (x ^â™­) (y ^â™­) â‰ƒ (x ^â™­) â‰ˆ (y ^â™­)
      step2 = (decode (x ^â™­) (y ^â™­))
        is-an-equivalence-because
        â™­-encode-decode-is-enough code encode decode (Î» {(xâ€² ^â™­) (yâ€² ^â™­) (refl ^â™­) â†’ refl}) (x ^â™­) (y ^â™­)
    in
      step2 âˆ˜â‰ƒ step1
